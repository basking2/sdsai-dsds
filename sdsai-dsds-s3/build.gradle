plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'eclipse'
}

dependencies {
    implementation project(':sdsai-dsds-core')
    implementation platform('software.amazon.awssdk:bom:2.5.29')
    implementation 'com.amazonaws:aws-java-sdk-s3:1.11.682'

    testImplementation 'junit:junit:4.10'
}

java {
    withJavadocJar()
    withSourcesJar()
}

if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
    publishing {
        repositories {
            maven {
                url = version.endsWith('SNAPSHOT') ? snapshotRepository : releaseRepository
                credentials {
                    username ossrhUsername
                    password ossrhPassword
                }
            }
        }

        publications {

            maven(MavenPublication) {
                groupId = group
                artifactId = 'sdsai-dsds-s3'

                from components.java

                pom {
                    name = "SDSAI DSDS S3"
                    description = 'Simple datastructures and algorithms data store data structures.'
                    url = 'https://github.com/basking2/sdsai-dsds'
                    scm {
                        connection = 'scm:git:git@github.com:basking2/sdsai-dsds.git'
                        developerConnection = 'git@github.com:basking2/sdsai-dsds.git'
                        url = 'https://github.com/basking2/sdsai-dsds.git'
                    }
                    licenses {
                        license {
                            name = "MIT"
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }

                    developers {
                        developer {
                            id = 'basking2'
                            name = 'Sam'
                            email = 'basking2@yahoo.com'
                        }
                    }
                }
            }
        }
    }

    signing {
        sign publishing.publications.maven
    }
}


