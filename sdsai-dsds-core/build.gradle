plugins {
  id 'java-library'
  id 'maven-publish'
  id 'signing'
  id 'eclipse'
}

dependencies {
  api 'org.slf4j:slf4j-api:1.6.4'
  api 'com.fasterxml.jackson.core:jackson-core:2.10.0'
  api 'com.fasterxml.jackson.core:jackson-databind:2.10.0'
  implementation 'org.slf4j:slf4j-log4j12:1.6.4'
  testImplementation 'junit:junit:4.10'
}

java {
  withJavadocJar()
  withSourcesJar()
}

if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
  publishing {
    repositories {
      maven {
        url = version.endsWith('SNAPSHOT') ? snapshotRepository : releaseRepository
        credentials {
          username ossrhUsername
          password ossrhPassword
        }
      }
    }

    publications {

      maven(MavenPublication) {
        groupId = group
        artifactId = 'sdsai-dsds-common'

        from components.java

        pom {
          name = "SDSAI DSDS Common"
          description = 'Simple datastructures and algorithms data store data structures.'
          url = 'https://github.com/basking2/sdsai-dsds'
          scm {
            connection = 'scm:git:git@github.com:basking2/sdsai-dsds.git'
            developerConnection = 'git@github.com:basking2/sdsai-dsds.git'
            url = 'https://github.com/basking2/sdsai-dsds.git'
          }
          licenses {
            license {
              name = "MIT"
              url = 'https://opensource.org/licenses/MIT'
            }
          }

          developers {
            developer {
              id = 'basking2'
              name = 'Sam'
              email = 'basking2@yahoo.com'
            }
          }
        }
      }
    }
  }

  signing {
    //sign publishing.publications.maven
    sign configurations.archives
  }
}


